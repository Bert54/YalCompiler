package yal.analyse ;

import java.util.*;
import yal.arbre.* ;
import yal.arbre.expressions.* ;
import yal.arbre.instructions.* ;
import yal.exceptions.AnalyseSyntaxiqueException;
import yal.tds.*;
import yal.tds.entree.*;
import yal.tds.symbole.*;

import java_cup.runtime.*;

action code {:
             :} ;

parser code {:

    public void report_error(String message, Object info) {

        HashMap<Integer, String> lesTerminaux = new HashMap<>() ;
    
        lesTerminaux.put(new Integer(CodesLexicaux.DEBUT), "debut") ;
        lesTerminaux.put(new Integer(CodesLexicaux.FIN), "fin") ;
        lesTerminaux.put(new Integer(CodesLexicaux.POINTVIRGULE), ";") ;

        StringBuffer m = new StringBuffer() ;

        if (info instanceof java_cup.runtime.Symbol) {
            java_cup.runtime.Symbol s = ((java_cup.runtime.Symbol) info);

            if (s.left >= 0) {                
                m.append("\tligne : " + (s.left + 1)) ;
                if (s.right >= 0)                    
                    m.append(" colonne : " + (s.right+1)) ;
            }
            
            if (s.value != null) {
                lesTerminaux.put(CodesLexicaux.CSTENTIERE, "" + s.value) ;
            }

            if (lesTerminaux.containsKey(new Integer(s.sym))) {
                m.append(" dernier token lu : " + lesTerminaux.get(new Integer(s.sym))) ;
            }
            else {
                m.append(" expression non termin√©e") ;
            }

        }
        throw new AnalyseSyntaxiqueException("" + m) ;
    }

    public void report_fatal_error(String message, Object info) {
        report_error(message, info);
    }
:};
   
/* ------------Declaration of Terminals and Non Terminals Section----------- */
   
/* Terminals (tokens returned by the scanner).  */

terminal Symbol    PROGRAMME, DEBUT, FIN, POINTVIRGULE, VIRGULE, FONCTION, ECRIRE, LIRE, ENTIER, EGALE, RETOURNER, TANTQUE, REPETER, FINTANTQUE, PAROUVRANTE, PARFERMANTE, SI, ALORS, SINON, FINSI, NON;
terminal String    IDF, CSTENTIERE, OPER, OPERT, OPELOGIQUE, OPELOGIQUET, OPERMULTET, OPERMULTOU, EXPNEG ;

/* Non terminals used in the grammar section.  */

non terminal String FONCTIONT;
non terminal ArbreAbstrait PROG, LINST, LINSTF, INST;
non terminal ExpressionBinaire EXP, EXPF, APPELFONCTION;
non terminal ExpressionLogique EXPLOG, EXPLOGF;
non terminal Instruction DECL_VAR, DECL_FONCTION, DECLARATION, AFFECT, BOUCLE, CONDITION, RETOURNE;
non terminal ArrayList<Idf> PARAMETRES, PARAMETRESDECL;
non terminal ArrayList<ExpressionBinaire> PAR_EFF;

//

precedence left OPERMULTOU;
precedence left OPERMULTET;
precedence left OPELOGIQUET;
precedence left OPELOGIQUE;
precedence left OPER;
precedence left OPERT;
precedence left EXPNEG;
precedence left PAROUVRANTE, PARFERMANTE;
precedence left NON;


/* ----------------------------Grammar Section-------------------- */
 
start with PROG ;

PROG    ::=     PROGRAMME  IDF  DEBUT  LINST:li FIN
                {: RESULT = li ; :}
                ;

LINST   ::=     LINST:li  INST:i
                {: ((BlocDInstructions)li).ajouter(i) ;
                   RESULT = li ; :}
        |
                INST:i
                {:
                   BlocDInstructions b = new BlocDInstructions(ileft + 1) ;
                   b.ajouter(i) ;
                   RESULT = b ; :}
        |
                LINST:li  DECLARATION:d
                {: ((BlocDInstructions)li).ajouter(d) ;
                RESULT = li ; :}
        |
                DECLARATION:d
                {:
                    BlocDInstructions b = new BlocDInstructions(dleft + 1) ;
                    b.ajouter(d) ;
                    RESULT = b ; :}
                ;

LINSTF  ::=     LINSTF:li  INST:i
                {: ((BlocDInstructions)li).ajouter(i) ;
                RESULT = li ; :}
        |
                INST:i
                {:
                BlocDInstructions b = new BlocDInstructions(ileft + 1) ;
                b.ajouter(i) ;
                RESULT = b ; :}
        |
                LINSTF:li  DECL_VAR:d
                {: ((BlocDInstructions)li).ajouter(d) ;
                RESULT = li ; :}
        |
                DECL_VAR:d
                {:
                    BlocDInstructions b = new BlocDInstructions(dleft + 1);
                    b.ajouter(d);
                    RESULT = b; :}
                ;

INST    ::=     ECRIRE EXP:e POINTVIRGULE
                {: RESULT = new Ecrire(e, eleft + 1) ; :}
        |
                LIRE IDF:i POINTVIRGULE
                {: RESULT = new Lire(ileft + 1, new Idf(ileft + 1, i)) ; :}
        |
                AFFECT:a POINTVIRGULE
                {: RESULT = a; :}
        |
                BOUCLE:b
                {: RESULT = b; :}
        |
                CONDITION:c
                {: RESULT = c; :}
        |
                RETOURNE:r POINTVIRGULE
                {: RESULT = r; :}
                ;

RETOURNE ::=    RETOURNER EXP:e
                {: RESULT = new Retourne(eleft+1, e); :}
                ;

BOUCLE  ::=     TANTQUE EXPLOG:el REPETER LINST:li FINTANTQUE
                {: RESULT = new BoucleWhile(elleft+1, li, el) ; :}
                ;

CONDITION ::=   SI EXPLOG:e ALORS LINST:lia FINSI
                {: RESULT = new Condition(eleft+1,e,lia); :}
        |
                SI EXPLOG:e ALORS LINST:lia SINON LINST:lib FINSI
                {: RESULT = new Condition(eleft+1,e,lia,lib); :}
                ;

AFFECT  ::=     IDF:i EGALE EXP:e
                {: RESULT = new Affectation(eleft+1, new Idf(ileft + 1, i), e); :}
                ;

EXP    ::=      EXP:g OPER:o EXPF:d
                {: RESULT = new ExpressionBinaire(gleft + 1, g, o, d); :}
       |
                EXP:g OPER:o EXPNEG EXPF:d
                {: RESULT = new ExpressionBinaire(gleft + 1, g, o, new ExpressionBinaireNegative(dleft+1, d)); :}
       |
                EXPNEG EXP:e
                {: RESULT = new ExpressionBinaireNegative(eleft + 1, e); :}
       |
                EXP:g OPER:o PAROUVRANTE EXP:d PARFERMANTE
                {: RESULT = new ExpressionBinaire(gleft + 1, g, o, d); :}
       |
                EXP:g OPERT:o EXPF:d
                {: RESULT = new ExpressionBinaire(gleft + 1, g, o, d); :}
       |
                EXP:g OPERT:o EXPNEG EXPF:d
                {: RESULT = new ExpressionBinaire(gleft + 1, g, o, new ExpressionBinaireNegative(dleft+1, d)); :}
       |
                EXP:g OPERT:o PAROUVRANTE EXP:d PARFERMANTE
                {: RESULT = new ExpressionBinaire(gleft + 1, g, o, d); :}
       |
                PAROUVRANTE EXP:e PARFERMANTE
                {: RESULT = e; :}
       |
                EXPF:e
                {: RESULT = e; :}
                ;

EXPLOG  ::=     EXPLOG:g OPERMULTET:o EXPLOGF:d
                {: RESULT = new ExpressionLogique(gleft+1, g, o, d); :}
       |
                EXPLOG:g OPERMULTOU:o EXPLOGF:d
                {: RESULT = new ExpressionLogique(gleft+1, g, o, d); :}
       |
                NON EXPLOG:e
                {: RESULT = new ExpressionLogiqueNon(eleft+1, e); :}
       |
                EXPLOG:g OPERMULTET:o PAROUVRANTE EXPLOGF:d PARFERMANTE
                {: RESULT = new ExpressionLogique(gleft+1, g, o, d); :}
       |
                EXPLOG:g OPERMULTOU:o PAROUVRANTE EXPLOGF:d PARFERMANTE
                {: RESULT = new ExpressionLogique(gleft+1, g, o, d); :}
       |
                PAROUVRANTE EXPLOG:e PARFERMANTE
                {: RESULT = e; :}
       |
                EXPLOGF:e
                {: RESULT = e; :}
                ;

EXPLOGF ::=     EXP:g OPELOGIQUE:o EXP:d
                {: RESULT = new ExpressionLogique(gleft+1, g, o, d); :}
       |
                EXP:g OPELOGIQUET:o EXP:d
                {: RESULT = new ExpressionLogique(gleft+1, g, o, d); :}
                ;

EXPF   ::=      CSTENTIERE:c
                {: RESULT = new ConstanteEntiere(c, cleft + 1) ; :}
       |
                APPELFONCTION:a
                {: RESULT = a; :}
       |
                IDF:i
                {: RESULT = new Idf(ileft + 1, i) ; :}
                ;

APPELFONCTION ::= IDF:i PAROUVRANTE PARFERMANTE
                {: RESULT = new AppelFonction(ileft + 1, i, new ArrayList<>()); :}
       |
                  IDF:i PAROUVRANTE PAR_EFF:pe PARFERMANTE
                {: RESULT = new AppelFonction(ileft + 1, i, pe); :}
                ;

PAR_EFF   ::= EXP:e VIRGULE PAR_EFF:pe
                {: pe.add(e);
                   RESULT = pe;
                :}
       |
                EXP:e
                {: ArrayList<ExpressionBinaire> al = new ArrayList<>();
                   al.add(e);
                   RESULT = al;
                :}
                ;

DECLARATION ::= DECL_VAR:d
                {: RESULT = d ; :}
       |
                DECL_FONCTION:d
                {:  Instruction in = d;
                    TDS.getInstance().sortieBloc();
                    RESULT = d; :}
                ;

DECL_FONCTION ::= FONCTIONT IDF:i PARAMETRES:p DEBUT LINSTF:li FIN
                {:  TDS.getInstance().ajouter(new EntreeFonction(i, ileft+1, p.size()), new SymboleFonction(TDS.getInstance().getTableLocaleCourante().getNumBloc()));
                    TDS.getInstance().getTableLocaleCourante().setNbParams(p.size());
                    RESULT = new DeclarerFonction(ileft+1, i, p, li);
                :}
                ;

FONCTIONT     ::= FONCTION
                {: TDS.getInstance().entreeBloc();:}
                ;

PARAMETRES    ::= PAROUVRANTE PARFERMANTE
                {: RESULT = new ArrayList<>(); :}
       |
                  PAROUVRANTE PARAMETRESDECL:pr PARFERMANTE
                {: RESULT = pr; :}
                ;

PARAMETRESDECL ::= ENTIER IDF:i POINTVIRGULE PARAMETRESDECL:pr
                {:  TDS.getInstance().getTableLocaleCourante().ajouter(new EntreeVariable(i, ileft+1), new SymboleVariable(Valeurs.getInstance().getTaillePile(TDS.getInstance().getTableLocaleCourante().getNumBloc())));
                    Valeurs.getInstance().empiler(TDS.getInstance().getTableLocaleCourante().getNumBloc());
                    pr.add(new Idf(ileft+1, i));
                    RESULT = pr;
                :}
       |
                ENTIER IDF:i
                {: ArrayList<Idf> al = new ArrayList<>();
                   TDS.getInstance().getTableLocaleCourante().ajouter(new EntreeVariable(i, ileft+1), new SymboleVariable(Valeurs.getInstance().getTaillePile(TDS.getInstance().getTableLocaleCourante().getNumBloc())));
                   Valeurs.getInstance().empiler(TDS.getInstance().getTableLocaleCourante().getNumBloc());
                   al.add(new Idf(ileft+1, i));
                   RESULT = al;
                :}
                ;

DECL_VAR ::=    ENTIER IDF:i POINTVIRGULE
                {: TDS.getInstance().getTableLocaleCourante().ajouter(new EntreeVariable(i, ileft+1), new SymboleVariable(Valeurs.getInstance().getTaillePile(TDS.getInstance().getTableLocaleCourante().getNumBloc())));
                   Valeurs.getInstance().empiler(TDS.getInstance().getTableLocaleCourante().getNumBloc());
                   RESULT = new Declarer(ileft+1, i);
                :}
                ;